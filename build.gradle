plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

tasks.compileJava.options.encoding = 'UTF-8'
tasks.compileJava.sourceCompatibility = JavaVersion.VERSION_1_8

group = 'net.leonardo_dgs'
version = '1.5.6'
description = 'Create cool interactive books!'

def mainPackage = 'net.leonardo_dgs.interactivebooks'

repositories {
    mavenCentral()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-chat:1.17-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.10'
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    implementation 'de.tr7zw:item-nbt-api:2.8.0'
    implementation 'net.kyori:adventure-text-minimessage:4.2.0-SNAPSHOT'
    implementation 'net.kyori:adventure-platform-bukkit:4.0.1-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    implementation 'com.github.Simplix-Softworks:SimplixStorage:3.2.3'
}

tasks.shadowJar {
    archiveBaseName.set(project.name)
    archiveClassifier.set('')
    archiveVersion.set('')
    minimize()

    relocate 'de.tr7zw.changeme.nbtapi', mainPackage + '.nbtapi'
    relocate 'net.kyori.adventure', mainPackage + '.adventure'
    relocate 'co.aikar.commands', mainPackage + '.acf'
    relocate 'co.aikar.locales', mainPackage + '.locales'
    relocate 'de.leonhard.storage', mainPackage + '.simplixstorage'
    relocate 'org.bstats', mainPackage + '.bstats'
}

processResources {
    expand(
            version: project.version,
            description: project.description,
            author: 'Leonardo_DGS',
            website: 'https://www.spigotmc.org/resources/45604/'
    )
}

tasks.jar.enabled = false

artifacts {
    archives shadowJar
}

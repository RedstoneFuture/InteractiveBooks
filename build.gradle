plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

tasks.compileJava.options.encoding = 'UTF-8'
tasks.compileJava.sourceCompatibility = JavaVersion.VERSION_1_8

group = 'net.leonardo_dgs'
version = '1.5.5'
description = 'Create cool interactive books!'

def mainPackage = 'net.leonardo_dgs.interactivebooks'

repositories {
    jcenter()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-chat:1.16-R0.4'
    compileOnly 'io.netty:netty-all:4.1.25.Final'
    compileOnly 'me.clip:placeholderapi:2.10.9'
    compileOnly 'org.projectlombok:lombok:1.18.18'
    annotationProcessor 'org.projectlombok:lombok:1.18.18'
    implementation 'de.tr7zw:item-nbt-api:2.7.1'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
}

tasks.shadowJar {
    archiveBaseName.set(project.name)
    archiveClassifier.set('')
    archiveVersion.set('')
    minimize()

    relocate 'de.tr7zw.changeme.nbtapi', mainPackage + '.nbtapi'
    relocate 'co.aikar.commands', mainPackage + '.acf'
    relocate 'co.aikar.locales', mainPackage + '.locales'
    relocate 'org.bstats', mainPackage + '.bstats'
}

processResources {
    expand(
            version: project.version,
            description: project.description,
            author: 'Leonardo_DGS',
            website: 'https://www.spigotmc.org/resources/45604/'
    )
}

tasks.jar.enabled = false

artifacts {
    archives shadowJar
}
